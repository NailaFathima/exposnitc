[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 13] = SP;
alias userSP R0;
userSP = SP;

//switching to kernel stack
SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 11]*512 - 1;

alias callNumber R1;
callNumber = [[PTBR + 2*(userSP-5)/512]*512 + (userSP-5)%512];

//setting mode flag
[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 9] = callNumber;

alias returnAddress R2;
returnAddress = [PTBR + 2*(userSP-1)/512]*512 + (userSP-1)%512;

//open system call
if (callNumber == 2) then
    breakpoint;
    alias counter R3;
    counter = 0;
    alias resourceTable R4;
    //resource table present at RESOURCE_TABLE_OFFSET places from the bottom of user area page
    resourceTable = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 11]*512 + RESOURCE_TABLE_OFFSET;

    //finding a free resourceTableEntry
    while (counter < 8) do
        if ([resourceTable + counter*2 + 0] == -1) then 
            break;
        endif;
        counter = counter+1;
    endwhile;
    if (counter == 16) then  //there is no free resource table entry - each resourceTableEntry = 2 words
        [returnAddress] = -3;
    else
        alias fileName R5;
        fileName = [[PTBR + 2*(userSP-4)/512]*512 + (userSP-4)%512];
        alias fileTableEntry R6;
        multipush(R0, R1, R2, R3, R4, R5); //open function - to retrive a free entry in the open file table. At once only 32 files can be open
        R1 = 3;
        R2 = fileName;
        call FILE_MANAGER;
        fileTableEntry = R0;
        multipop(R0, R1, R2, R3, R4, R5);

        if (fileTableEntry < 0) then 
            [returnAddress] = -2;
        else  
            [resourceTable + counter*2 + 0] = FILE;  
            [resourceTable + counter*2 + 1] = fileTableEntry;
            [returnAddress] = counter;  //resource table index is the file descriptor
        endif;
    endif;
    breakpoint;
endif;

//close system call
if (callNumber == 3) then 
    breakpoint;
    alias fileDescriptor R3;
    fileDescriptor = [[PTBR + 2*(userSP-4)/512]*512 + (userSP-4)%512];
    if (fileDescriptor < 0 || fileDescriptor > 7) then //invalid resourceTable index
        [returnAddress] = -1;
    else 
        alias resourceTableEntry R4;
        resourceTableEntry = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 11]*512 + RESOURCE_TABLE_OFFSET + fileDescriptor*2;

        if ([resourceTableEntry + 0] != FILE) then
            [returnAddress] = -1;
        else
            alias openFileTableEntry R5;
            openFileTableEntry = [resourceTableEntry + 1];  

            multipush(R0, R1, R2, R3, R4, R5); //close function
            R1 = 4;
            R2 = openFileTableEntry;
            call FILE_MANAGER;
            multipop(R0, R1, R2, R3, R4, R5);

            [resourceTableEntry + 0] = -1;  //set the resource table entry to null
            [returnAddress] = 0;
        endif;
    endif;
    breakpoint;
endif;

[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16) + 9] = 0; //reset the mode flag
SP = userSP;
ireturn;