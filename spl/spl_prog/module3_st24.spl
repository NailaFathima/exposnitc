alias functionNumber R1;

//BUFFER READ
if (functionNumber == 2) then
    breakpoint;
    alias blockNumber R2;
    alias offset R3;
    alias memAddress R4;
    alias bufferIndex R5;
    bufferIndex = blockNumber % MAX_BUFFER; 

    multipush(R0, R1, R2, R3, R4, R5);
    R1 = 1;
    R2 = bufferIndex;   //acquire buffer
    R3 = [SYSTEM_STATUS_TABLE+1];
    call RESOURCE_MANAGER;
    multipop(R0, R1, R2, R3, R4, R5);

    if ([BUFFER_TABLE + bufferIndex*4 + 0] != blockNumber) then //if buffer contains a different block
        if ([BUFFER_TABLE + bufferIndex*4 + 1] == 1) then  //dirty bit is set, then call disk store
            multipush(R0, R1, R2, R3, R4, R5);
            R1 = 1;
            R2 = [SYSTEM_STATUS_TABLE+1];
            R2 = 71 + bufferIndex;
            R3 = [BUFFER_TABLE + bufferIndex*4 + 0];
            call DEVICE_MANAGER;
            multipop(R0, R1, R2, R3, R4, R5);

            [BUFFER_TABLE + bufferIndex*4 + 1] = 0;  //unmark the dirty bit
        endif;
        multipush(R0, R1, R2, R3, R4, R5);
        R1 = 2;
        R4 = blockNumber;  //load the desired block in buffer using disk_load function
        R2 = [SYSTEM_STATUS_TABLE+1];
        R3 = 71 + bufferIndex;
        call DEVICE_MANAGER;
        multipop(R0, R1, R2, R3, R4, R5);

        [BUFFER_TABLE + bufferIndex*4 + 0] = blockNumber;  //set the new disk block number
    endif;

    [memAddress] = [(71 + bufferIndex)*512 + offset];  //copy the contents in the offset loaction in buffer to memory address
    multipush(R0, R1, R2, R3, R4, R5);
    R1 = 2;
    R2 = bufferIndex;
    R3 = [SYSTEM_STATUS_TABLE+1];  //release buffer
    call RESOURCE_MANAGER;
    multipop(R0, R1, R2, R3, R4, R5);
    breakpoint;
endif;

//OPEN FUNCTION
if (functionNumber == 3) then
    breakpoint;
    alias fileName R2;
    alias counter R3;
    counter = 0;
    while (counter < MAX_FILE_NUM && [INODE_TABLE + counter*16 + 1]  != fileName) do
        counter = counter+1;
    endwhile;
    if (counter == MAX_FILE_NUM) then  //counter = index of the inode table entry
        R0 = -1;
    else
        alias returnValue R4; 
        multipush(R0, R1, R2, R3);  //Acquire Inode function to ensure that the file doesnt get deleted in the meanwhile.
        R1 = 4;
        R2 = counter;
        R3 = [SYSTEM_STATUS_TABLE+1];
        call RESOURCE_MANAGER;
        returnValue = R0;
        multipop(R0, R1, R2, R3);

        if (returnValue == -1) then 
            R0 = -1;
        else
            if ([INODE_TABLE + counter*16 + 0] == EXEC) then  //if its exec file, then we cant open it, so Release Inode
                multipush(R0, R1, R2, R3, R4);
                R1 = 5;
                R2 = counter;
                R3 = [SYSTEM_STATUS_TABLE+1];
                call RESOURCE_MANAGER;
                multipop(R0, R1, R2, R3, R4);

                R0 = -1;  
            else
                alias openFileEntry R5;  //find an entry in the open file table; MAX_OPEN_FILE = 32
                openFileEntry = 0;
                while (openFileEntry < MAX_OPENFILE_NUM) do 
                    if ([OPEN_FILE_TABLE + openFileEntry*4 + 0] == -1) then
                        break;
                    endif;
                    openFileEntry = openFileEntry + 1;
                endwhile;

                if (openFileEntry == MAX_OPENFILE_NUM) then //no free entry found in open table
                    R0 = -2;
                else
                    if (fileName == "root") then
                        [OPEN_FILE_TABLE + openFileEntry*4 + 0] = INODE_ROOT; //no need to increment the file_open_count
                    else
                        if ([FILE_STATUS_TABLE + counter*4 + 1] == -1) then 
                            [FILE_STATUS_TABLE + counter*4 + 1] = 1;
                        else
                            [FILE_STATUS_TABLE + counter*4 + 1] = [FILE_STATUS_TABLE + counter*4 + 1] + 1;
                        endif;
                        [OPEN_FILE_TABLE + openFileEntry*4 + 0] = counter;  //set the inode index in the open file table entry
                    endif;
                    [OPEN_FILE_TABLE + openFileEntry*4 + 1] = 1;  //share_count will be incremented cos of fork 
                    [OPEN_FILE_TABLE + openFileEntry*4 + 2] = 0;  //lseek
                    
                    multipush(R0, R1, R2, R3, R4, R5);  //Release Inode
                    R1 = 5;
                    R2 = counter;
                    R3 = [SYSTEM_STATUS_TABLE+1];
                    call RESOURCE_MANAGER;
                    multipop(R0, R1, R2, R3, R4, R5);

                    R0 = openFileEntry;
                endif;
            endif;
        endif;
    endif;
    breakpoint;
endif;

//Close function
if (functionNumber == 4) then
    breakpoint;
    alias openFileEntry R2;  //OPEN_FILE_TABLE index is given as input
    alias inodeIndex R3;
    inodeIndex = [OPEN_FILE_TABLE + openFileEntry*4 + 0];
    [OPEN_FILE_TABLE + openFileEntry*4 + 1] = [OPEN_FILE_TABLE + openFileEntry*4 + 1] - 1; //decrement the count

    if ([OPEN_FILE_TABLE + openFileEntry*4 + 1] == 0) then
        [OPEN_FILE_TABLE + openFileEntry*4 + 1] = -1; //set all fields to -1
        alias counter R4;
        counter = 0;
        while (counter < 4) do
            [OPEN_FILE_TABLE + openFileEntry*4 + counter] = -1;
            counter = counter+1;
        endwhile;
        if (inodeIndex != INODE_ROOT) then
            [FILE_STATUS_TABLE + inodeIndex*4 + 1] = [FILE_STATUS_TABLE + inodeIndex*4 + 1] - 1;
            if ([FILE_STATUS_TABLE + inodeIndex*4 + 1] == 0) then //make it a free entry
                [FILE_STATUS_TABLE + inodeIndex*4 + 1] = -1;
            endif;
        endif;
    endif;
    breakpoint;
endif;

return;