alias userSP R0;
userSP=SP;

//UPTR set to Stack Ptr
[PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 13] = SP;

//Stack Pointer set to bottom UA Page No.
SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 11] * 512 - 1;

//Set MODE Flag to 9
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 9;

//Accessing physical File Name location

alias physicalPageNum R1;
alias offset R2;
alias fileNamePhysicalAddr R3;

//Try to find fileNamePhysicalAddr and the fileName

physicalPageNum = [PTBR + 2 * ((userSP - 4)/ 512)];
offset = (userSP - 4) % 512;
fileNamePhysicalAddr = (physicalPageNum * 512) + offset;
alias fileName R4;
fileName=[fileNamePhysicalAddr];



//Checking if FileName present in Inode Table

alias fileInodeIndex R5;
fileInodeIndex = 0;

while(fileInodeIndex < MAX_FILE_NUM )do
    if ( [ INODE_TABLE + ( fileInodeIndex * 16 ) + 1 ] == fileName )then
        break;
    endif;
    fileInodeIndex = fileInodeIndex + 1;
endwhile;


//If no such file exists or XEXE format is not applicable
if ( fileInodeIndex == MAX_FILE_NUM || [ INODE_TABLE + ( fileInodeIndex * 16 ) + 0 ] != EXEC )then
    alias physicalAddrRetVal R6;
    physicalAddrRetVal = ([PTBR + 2 * ((userSP - 1) / 512)] * 512) + ((userSP - 1) % 512);
    [physicalAddrRetVal] = -1;

    // Reset Mode Flag
    [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 0;

    //Reset Stack pointer
    SP = [PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 13];
    ireturn;
endif;



//Calling the exit process with func no. = 3 

multipush(R0,R1,R2,R3,R4,R5,R6,PTBR,PTLR);

R1 = 3;
R2 = [SYSTEM_STATUS_TABLE + 1];
call PROCESS_MANAGER;

multipop(R0,R1,R2,R3,R4,R5,R6,PTBR,PTLR);


//Reclaiming UAPage
alias UAPage R7;
UAPage = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 11];
[MEMORY_FREE_LIST + UAPage] = [MEMORY_FREE_LIST + UAPage] + 1;
[SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2] - 1;

//New Kernel Stack initialised
SP = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 11] * 512 - 1;

//STATE field set
[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 4] = RUNNING;

//Inode index field of Process Table updated with new process for exec() needs it to load it from the disk
[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 7] = fileInodeIndex;

//Allocating new pages

PTBR = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 14];
PTLR = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 15];

//Library
[PTBR+0] = 63;
[PTBR+1] = "0100";
[PTBR+2] = 64;
[PTBR+3] = "0100";

//Heap
multipush(R0,R1,R2,R3,R4,R5,R6,R7,PTBR,PTLR);
R1 = 1;
call MEMORY_MANAGER;
[PTBR+4] = R0;
multipop(R0,R1,R2,R3,R4,R5,R6,R7,PTBR,PTLR);
[PTBR+5] = "0110";

multipush(R0,R1,R2,R3,R4,R5,R6,R7,PTBR,PTLR);
R1 = 1;
call MEMORY_MANAGER;
[PTBR+6] = R0;
multipop(R0,R1,R2,R3,R4,R5,R6,R7,PTBR,PTLR);
[PTBR+7] = "0110";

//Code
[PTBR+8]  = -1;
[PTBR+9]  = "0000";
[PTBR+10] = -1;
[PTBR+11] = "0000";
[PTBR+12] = -1;
[PTBR+13] = "0000";
[PTBR+14] = -1;
[PTBR+15] = "0000";

alias countOfPages R8;
countOfPages = 0;
R9 = 8;
while(R9 != 12)do
    if ( [ INODE_TABLE + ( fileInodeIndex * 16 ) + R9 ] != -1 )then 
        countOfPages = countOfPages + 1;
    endif;
    R9 = R9 + 1;
endwhile;

R9 = 0;
while( R9 != countOfPages )do
    multipush(R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,PTBR,PTLR);
    R1 = 1;
    call MEMORY_MANAGER;
    [PTBR + 8 + ( 2 * R9 ) ] = R0;
    multipop(R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,PTBR,PTLR);
    [PTBR + 8 + ( 2 * R9 ) + 1 ] = "0100";
    R9 = R9 + 1;
endwhile;




//Stack
multipush(R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,PTBR,PTLR);
R1 = 1;
call MEMORY_MANAGER;
[PTBR + 16 ] = R0;
multipop(R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,PTBR,PTLR);
[PTBR + 17 ] = "0110";

multipush(R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,PTBR,PTLR);
R1 = 1;
call MEMORY_MANAGER;
[PTBR + 18 ] = R0;
multipop(R0,R1,R2,R3,R4,R5,R6,R7,R8,R9,PTBR,PTLR);
[PTBR + 19 ] = "0110";

//Loading code blocks
R9 = 0;
while( R9 != countOfPages )do   
    loadi([PTBR + 8 + ( 2 * R9 ) ],[ INODE_TABLE + ( fileInodeIndex * 16 ) + 8 + R9 ]);
    R9 = R9 + 1;
endwhile;

[[PTBR + 16] * 512] = [[PTBR + 8] * 512 + 1];


// Reset Mode Flag
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 0;

SP = 8 * 512;
ireturn;
